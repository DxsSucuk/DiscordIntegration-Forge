buildscript {
    repositories {
        mavenCentral()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
        repositories {
            maven {
                url "https://plugins.gradle.org/m2/"

            }
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.4"
    }
}

//Gradle Plugins
plugins {
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id "com.wynprice.cursemaven" version "2.1.5"
    id 'java'
    id "com.modrinth.minotaur" version "1.2.1"
}
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'


//Build variables
version = "2.4.7.1"
group = "de.erdbeerbaerlp.dcintegration"
archivesBaseName = "dcintegration-forge"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}


//Gradle Repositories
repositories {
    maven { url 'https://jitpack.io' }
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven'
    }
    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }// Maybe needed for other mods in the future
    //maven { url "https://maven.latmod.com/" } //FTB
    maven { url "https://repository.dev.gotan.io/repository/gotan.os/" }
    maven { url "https://repo.opencollab.dev/maven-snapshots" }
    maven { url "https://repo.opencollab.dev/maven-releases" }
    maven {
        name = 'sonatype-oss'
        url = 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
    maven {
        name = 'howaner'
        url = "http://repo.howaner.de/"
    }
    maven { url "https://repo.erdbeerbaerlp.de/repository/maven-public/" }
}

//Forge
minecraft {
    mappings channel: 'snapshot', version: '20171003-1.12'
    runs {
        server {
            workingDirectory project.file('run')
            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
            mods {
                dcintegration {
                    source sourceSets.main
                }
            }
        }
    }
}


//ShadowJar
configurations {
    embed
    compile.extendsFrom(embed)
}
task release {

}
shadowJar {
    classifier = '1.12'
    configurations = [project.configurations.embed]
    relocate 'org.slf4j', 'dcshadow.slf4j'
    exclude('module-info.class', 'META-INF/versions/9/module-info.class')
    // Keeping them causes errors when loaded by forge
}
reobf {
    shadowJar {
        dependsOn tasks.createMcpToSrg
        mappings = tasks.createMcpToSrg.outputs.files.singleFile
    }
}
artifacts {
    archives tasks.shadowJar
}
configurations.all {
    resolutionStrategy {
        cacheChangingModulesFor 5, 'minutes'
    }
}
//Dependencies
dependencies {

    // =========  Common  =====================
    embed('de.erdbeerbaerlp:dcintegration.common:2.4.7-j8') {
        changing = true
    }

    // =========  Minecraft Forge =============
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2855'

    //Mixin
    annotationProcessor 'org.spongepowered:mixin:0.8.2:processor'

    //Dynmap-API
    compileOnly('com.github.webbukkit:DynmapCoreAPI:2.5')

    //Votifier Forge
    compile 'com.github.upcraftlp:votifier:master-SNAPSHOT'

    //Mixin
    embed("org.spongepowered:mixin:0.8-SNAPSHOT") {
        transitive = false
    }
}


//Mixin
mixin {
    add sourceSets.main, 'mixins.dcintegration.refmap.json'
}

//Manifest attributes
jar {
    classifier = "1.12"
    manifest {
        attributes([
                'Maven-Artifact'             : "${project.group}:${project.archivesBaseName}:${project.version}",
                'Timestamp'                  : System.currentTimeMillis(),
                "Specification-Title"        : "dcintegration",
                "Specification-Vendor"       : "dcintegration",
                "Specification-Version"      : "1", // We are version 1 of ourselves
                "Implementation-Title"       : project.name,
                "Implementation-Version"     : "${version}",
                "Implementation-Vendor"      : "dcintegration",
                "Implementation-Timestamp"   : new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'MixinConnector'             : 'de.erdbeerbaerlp.dcintegration.forge.DCMixinConnector',
                'TweakClass'                 : 'org.spongepowered.asm.launch.MixinTweaker',
                'MixinConfigs'               : 'mixins.dcintegration.json',
                "ForceLoadAsMod"             : "true",
                'FMLCorePluginContainsFMLMod': 'true'
        ])
    }

}

//Curseforge publishing
curseforge {
    if (project.hasProperty('curseforge.apikey')) { // $GRADLE_USER_HOME/gradle.properties
        apiKey = getProperty("curseforge.apikey")
        project {
            id = '324952'
            changelog = project.changelog
            releaseType = 'release'
            addGameVersion '1.12.2'
            mainArtifact(jar) {
                displayName = "DiscordIntegration-Forge $version (MC 1.12.2)"
            }
            relations {
                optionalDependency 'voting'
                requiredDependency 'mixinbootstrap'
            }
        }
    }
}
import com.modrinth.minotaur.TaskModrinthUpload

task publishModrinth(type: TaskModrinthUpload) {

    if (project.hasProperty('modrinth.apikey')) { // $GRADLE_USER_HOME/gradle.properties
        token = getProperty("modrinth.apikey") // Use an environment property!
        projectId = 'rbJ7eS5V'
        versionNumber = version+"1.12.2"
        versionName = "DiscordIntegration-Forge $version (MC 1.12.2)"
        uploadFile = shadowJar
        changelog = project.changelog
        addGameVersion('1.12.2')
        addLoader('forge')
        versionType = "RELEASE"
    }
}

tasks.release.dependsOn(tasks.build, tasks.curseforge, tasks.publishModrinth)
